plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0' apply false
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'
    group = 'dev.esophose'
    version = '1.0.0-SNAPSHOT'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        jcenter()

        maven { url = 'https://repo.codemc.org/repository/nms' }
        maven { url = 'https://repo.codemc.org/repository/maven-public' }
        maven { url = 'https://repo.aikar.co/content/groups/aikar/' }
        maven { url = 'http://repo.extendedclip.com/content/repositories/placeholderapi/' }
        maven { url = 'http://maven.playpro.com' }
    }
}

configurations {
    childJars
}

dependencies {
    implementation project(':Plugin')
    implementation project(':NMS-Wrapper')
    implementation project(':NMS-v1_13_R2')
    implementation project(':NMS-v1_14_R1')
    implementation project(':NMS-v1_15_R1')

    subprojects.each {
        childJars project(it.path)
    }
}

shadowJar {
    archiveClassifier.set(null)

    relocate('com.zaxxer.hikari', 'dev.esophose.rosestacker.lib.hikaricp')
    relocate('org.slf4j', 'dev.esophose.rosestacker.lib.slf4j')
    relocate('co.aikar.commands', 'dev.esophose.rosestacker.lib.acf.commands')
    relocate('co.aikar.locales', 'dev.esophose.rosestacker.lib.acf.locales')
    //relocate('co.aikar.timings', 'dev.esophose.rosestacker.lib.acf.timings')
    relocate('co.aikar.util', 'dev.esophose.rosestacker.lib.acf.util')
    relocate('net.jodah.expiringmap', 'dev.esophose.rosestacker.lib.expiringmap')
    relocate('org.intellij', 'dev.esophose.rosestacker.lib.intellij')
    relocate('org.jetbrains', 'dev.esophose.rosestacker.lib.jetbrains')

    exclude 'acf-*.properties'
}

jar {
    dependsOn configurations.childJars
    from {
        configurations.childJars.collect {
            zipTree(it)
        }
    }
}

build.dependsOn shadowJar
