plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0' apply false
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'
    group = 'dev.rosewood'
    version = 'Beta-61'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        jcenter()

        maven { url = 'https://repo.codemc.org/repository/nms' }
        maven { url = 'https://repo.codemc.org/repository/maven-public' }
        maven { url = 'https://repo.aikar.co/content/groups/aikar/' }
        maven { url = 'http://repo.extendedclip.com/content/repositories/placeholderapi/' }
        maven { url = 'http://maven.playpro.com' }
        maven { url = 'https://repo.rosewooddev.io/repository/public/' }

        maven {
            url = 'https://repo.rosewooddev.io/repository/maven-public'
            credentials {
                username project.mavenUser
                password project.mavenPassword
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
    }
}

configurations {
    childJars
}

dependencies {
    implementation project(':Plugin')
    implementation project(':NMS-Wrapper')
    implementation project(':NMS-v1_13_R2')
    implementation project(':NMS-v1_14_R1')
    implementation project(':NMS-v1_15_R1')
    implementation project(':NMS-v1_16_R1')

    subprojects.each {
        childJars project(it.path)
    }
}

shadowJar {
    archiveClassifier.set(null)

    relocate('dev.rosewood.guiframework', 'dev.rosewood.rosestacker.lib.guiframework')
    relocate('com.zaxxer.hikari', 'dev.rosewood.rosestacker.lib.hikaricp')
    relocate('org.slf4j', 'dev.rosewood.rosestacker.lib.slf4j')
    relocate('co.aikar.commands', 'dev.rosewood.rosestacker.lib.acf.commands')
    relocate('co.aikar.locales', 'dev.rosewood.rosestacker.lib.acf.locales')
    //relocate('co.aikar.timings', 'dev.rosewood.rosestacker.lib.acf.timings')
    relocate('co.aikar.util', 'dev.rosewood.rosestacker.lib.acf.util')
    relocate('net.jodah.expiringmap', 'dev.rosewood.rosestacker.lib.expiringmap')
    relocate('org.intellij', 'dev.rosewood.rosestacker.lib.intellij')
    relocate('org.jetbrains', 'dev.rosewood.rosestacker.lib.jetbrains')

    exclude 'acf-*.properties'
}

jar {
    dependsOn configurations.childJars
    from {
        configurations.childJars.collect {
            zipTree(it)
        }
    }
}

build.dependsOn shadowJar
