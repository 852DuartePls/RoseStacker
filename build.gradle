import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'java'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = 'dev.esophose'
version = '1.0.0'

repositories {
    mavenCentral()
    jcenter()

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public' }
    maven { url = 'https://repo.codemc.org/repository/nms' }
    maven { url = 'https://repo.aikar.co/content/groups/aikar/' }

    // Need to find a better maven host for Holograms and CMI
    maven {
        credentials  {
            username = mavenUser
            password = mavenPassword
        }
        url = 'https://repo.songoda.com/artifactory/private/'
    }
}

compileJava {
    options.compilerArgs += ['-parameters']
    options.fork = true
    options.forkOptions.executable = 'javac'
}

dependencies {
    // Referenced dependencies
    shadow 'org.spigotmc:spigot:1.15.2-R0.1-20200202.085145-8'
    shadow 'com.gmail.filoghost.holographicdisplays:holographicdisplays-api:2.3.2'
    shadow 'com.sainttx.holograms:Holograms:2.9.1'
    shadow 'com.zrips:CMI:8.4.0.2_1'

    // Referenced stack plugin dependencies
    shadow 'com.songoda:UltimateStacker:1.9.6'
    shadow 'com.bgsoftware:WildStacker:2.9.0'
    shadow 'uk.antiperson:stackmob:4-0-2'

    // Dependencies that will be shaded into the jar
    implementation 'co.aikar:acf-paper:0.5.0-SNAPSHOT'
    implementation 'com.zaxxer:HikariCP:3.2.0'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'org.slf4j:slf4j-nop:1.7.25'
}

shadowJar {
    archiveClassifier.set(null)

    relocate('com.zaxxer.hikari', 'dev.esophose.sparkstacker.lib.hikaricp')
    relocate('org.slf4j', 'dev.esophose.sparkstacker.lib.slf4j')
    relocate('co.aikar.commands', 'dev.esophose.sparkstacker.lib.acf')

    exclude 'acf-*.properties'
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter ReplaceTokens, tokens: ["version": project.property("version")]
    }
}

build.dependsOn shadowJar
