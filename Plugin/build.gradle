import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'maven-publish'
}

compileJava {
    options.compilerArgs += ['-parameters']
    options.fork = true
    options.forkOptions.executable = 'javac'
    options.encoding = 'UTF-8'
}

dependencies {
    // Other modules
    implementation project(':NMS-Wrapper')

    // Referenced dependencies
    shadow 'org.spigotmc:spigot:1.16.1'
    shadow 'me.clip:placeholderapi:2.10.4'
    shadow 'net.coreprotect:coreprotect:2.15.0'
    shadow 'com.gmail.filoghost.holographicdisplays:holographicdisplays-api:2.4.0'

    // From our repo
    shadow 'com.bgsoftware:wildstacker:3.0.2'
    shadow 'com.gmail.filoghost:holographicdisplays:2.4.2'
    shadow 'com.sainttx:holograms:2.12'
    shadow 'com.songoda:epicspawners:6.3.9'
    shadow 'com.songoda:ultimatestacker:1.11.10'
    shadow 'me.minebuilders:clearlag:3.1.6'
    shadow 'net.brcdev:shopgui:1.29.0'
    shadow 'uk.antiperson:stackmob:5.1.0'

    // Dependencies that will be shaded into the jar
    implementation 'dev.rosewood:guiframework:1.1.0'
    implementation 'co.aikar:acf-paper:0.5.0-SNAPSHOT'
    implementation 'com.zaxxer:HikariCP:3.2.0'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'org.slf4j:slf4j-nop:1.7.25'
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter ReplaceTokens, tokens: ["version": project.property("version")]
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
        mavenJava(MavenPublication) {
            artifactId = 'rosestacker'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'rosestacker'
            }
        }
    }
    repositories {
        if (project.hasProperty('mavenUsername') && project.hasProperty('mavenPassword')) {
            maven {
                credentials {
                    username project.mavenUsername
                    password project.mavenPassword
                }

                def releasesRepoUrl = 'https://repo.codemc.org/repository/maven-releases/'
                def snapshotsRepoUrl = 'https://repo.codemc.org/repository/maven-snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}
